apply plugin: 'java'
project.ext.set('natives', "$buildDir/libs/natives")
group 'com.zerozipp'
version '2.0'

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

configurations { bundle }
repositories {
    mavenCentral()
    maven {
        url 'https://libraries.minecraft.net/'
        metadataSources { artifact() }
    }

    ivy {
        url 'https://launcher.mojang.com/'
        metadataSources { artifact() }
        patternLayout {
            artifact '/v1/objects/[organisation]/[module].[ext]'
            m2compatible = true
        }
    }
}

dependencies {
    implementation '0f275bc1547d01fa5f56ba34bdc87d981ee12daf:client'
    implementation 'net.minecraft:launchwrapper:1.12'
    implementation 'org.ow2.asm:asm-all:5.2'
    implementation 'com.mojang:patchy:1.3.9'
    implementation 'oshi-project:oshi-core:1.1'
    implementation 'net.java.dev.jna:jna:4.4.0'
    implementation 'net.java.dev.jna:platform:3.4.0'
    implementation 'com.ibm.icu:icu4j-core-mojang:51.2'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.3'
    implementation 'com.paulscode:codecjorbis:20101023'
    implementation 'com.paulscode:codecwav:20101023'
    implementation 'com.paulscode:libraryjavasound:20101123'
    implementation 'com.paulscode:librarylwjglopenal:20100824'
    implementation 'com.paulscode:soundsystem:20120107'
    implementation 'io.netty:netty-all:4.1.9.Final'
    implementation 'com.google.guava:guava:21.0'
    implementation 'org.apache.commons:commons-lang3:3.5'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-codec:commons-codec:1.10'
    implementation 'net.java.jinput:jinput:2.0.5'
    implementation 'net.java.jutils:jutils:1.0.0'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.mojang:authlib:1.5.25'
    implementation 'com.mojang:realms:1.10.22'
    implementation 'org.apache.commons:commons-compress:1.8.1'
    implementation 'org.apache.httpcomponents:httpclient:4.3.3'
    implementation 'commons-logging:commons-logging:1.1.3'
    implementation 'org.apache.httpcomponents:httpcore:4.3.2'
    implementation 'it.unimi.dsi:fastutil:7.1.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.8.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.8.1'
    implementation 'org.lwjgl.lwjgl:lwjgl:2.9.4-nightly-20150209'
    implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.4-nightly-20150209'
    implementation 'com.mojang:text2speech:1.10.3'
    implementation 'ca.weblite:java-objc-bridge:1.0.0'
    bundle 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209:natives-linux'
    bundle 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209:natives-osx'
    bundle 'org.lwjgl.lwjgl:lwjgl-platform:2.9.4-nightly-20150209:natives-windows'
    bundle 'net.java.jinput:jinput-platform:2.0.5:natives-linux'
    bundle 'net.java.jinput:jinput-platform:2.0.5:natives-osx'
    bundle 'net.java.jinput:jinput-platform:2.0.5:natives-windows'
    bundle 'com.mojang:text2speech:1.10.3:natives-linux'
    bundle 'com.mojang:text2speech:1.10.3:natives-windows'
}

task extractNatives(type: Sync) {
    from configurations.bundle.collect { zipTree(it) }
    into file(project.natives)
    exclude 'META-INF/*'
    exclude 'META-INF'
}

task runClient(type: JavaExec) {
    dependsOn extractNatives
    classpath = sourceSets.main.runtimeClasspath
    systemProperty 'java.library.path', project.natives
    args = ['--username', 'Debug', '--version', 'Aurora', '--accessToken', '0', '--tweakClass', 'com.zerozipp.client.loader.Tweaker']
    mainClass = 'net.minecraft.launchwrapper.Launch'
    workingDir = 'game'
}